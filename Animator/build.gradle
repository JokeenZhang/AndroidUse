apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "com.zzq.animator"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    flavorDimensions "animatorType", "mode"

    productFlavors {

        viewPropertyType {
            dimension "animatorType"
            versionNameSuffix "viewPropertyType-"
        }

        activityType {
            dimension "animatorType"
            versionNameSuffix "activityType-"
        }

        dialogType {
            dimension "animatorType"
            versionNameSuffix "dialogType-"
        }

        fragmentType {
            dimension "animatorType"
            versionNameSuffix "fragmentType"
        }

        dialogFragmentType {
            dimension "animatorType"
            versionNameSuffix "dialogFragmentType"
        }

        //正式版本
        official {
            dimension "mode"
        }

        selfUse {
            dimension "mode"
        }
    }

    sourceSets {
        viewPropertyType {
            java.srcDirs = ['src/viewPropertyType/java']
            manifest.srcFile 'src/viewPropertyType/AndroidManifest.xml'
        }
    }

    signingConfigs {
        release {
            def releaseKeyFile = file('../keyStore/release.properties')
            if (releaseKeyFile.canRead()) {
                Properties props = new Properties()
                props.load(new FileInputStream(releaseKeyFile))
                if (props != null && props.containsKey('STORE_FILE') && props.containsKey('STORE_PASSWORD') &&
                        props.containsKey('KEY_ALIAS') && props.containsKey('KEY_PASSWORD')) {
                    storeFile = file(props['STORE_FILE'])
                    storePassword = props['STORE_PASSWORD']
                    keyAlias = props['KEY_ALIAS']
                    keyPassword = props['KEY_PASSWORD']
                } else {
                    println 'signing.properties found but some entries are missing'
                    android.buildTypes.release.signingConfig = null
                }
            } else {
                println 'signing.properties not found'
                android.buildTypes.release.signingConfig = null
            }
        }

        debug {
            def debugKeyFile = file('../keyStore/debug.properties')
            if (debugKeyFile.canRead()) {
                Properties props = new Properties()
                props.load(new FileInputStream(debugKeyFile))
                if (props != null && props.containsKey('STORE_FILE') && props.containsKey('STORE_PASSWORD') &&
                        props.containsKey('KEY_ALIAS') && props.containsKey('KEY_PASSWORD')) {
                    storeFile = file(props['STORE_FILE'])
                    storePassword = props['STORE_PASSWORD']
                    keyAlias = props['KEY_ALIAS']
                    keyPassword = props['KEY_PASSWORD']
                } else {
                    println 'signing.properties found but some entries are missing'
                    android.buildTypes.debug.signingConfig = null
                }
            } else {
                println 'signing.properties not found'
                android.buildTypes.debug.signingConfig = null
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            signingConfig  signingConfigs.release
            buildConfigField "boolean", "isDebug", "false"
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            minifyEnabled false
            signingConfig  signingConfigs.debug
            buildConfigField "boolean", "isDebug", "true"
        }
    }

    //过滤不需要的构建变体
    variantFilter { variant ->
        def names = variant.flavors*.name
        def buildTypeName = variant.buildType.name

        println "变种维度有： " + names + " buildType：" + buildTypeName
//        println "起来：" + variant
//        println "还有：" + variant.flavors
//        println "我在：" + buildTypes
        // To check for a build type instead, use variant.buildType.name == "buildType"

        if (buildTypeName.contains("release")) {
            if (!names.contains("official")) {
                setIgnore(true)
            }
        }else if (buildTypeName.contains("debug")) {
            if (names.contains("official")) {
                setIgnore(true)
            }
        }
    }

    buildFeatures {
        dataBinding = true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    testImplementation deps.test.junit
    androidTestImplementation deps.test.ext_junit
    androidTestImplementation deps.espresso.core
    implementation project(path: ':common')

    implementation deps.kotlin.stdlib
    implementation deps.core_ktx
    implementation deps.app_compat
    implementation deps.androidx_view.constraintlayout

    implementation deps.google.material
    implementation deps.lifecycle.extension

}